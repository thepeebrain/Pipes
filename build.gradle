buildscript {
	repositories {
		jcenter()
		maven { url "http://files.minecraftforge.net/maven" }
	}

	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

ext {
	modid = "lazylazulipipes"
	modname = "Lazy Lazuli's Pipes"

	modfile = "Pipes.java"

	mcversion = "1.12"
	forgeversion = "14.21.0.2331"
	libVersion = "$mcversion-2.0.0-56"
	modversion = "2.0.0"

	getBuildNumber = { ->
		stdout = new ByteArrayOutputStream()
		exec {
			commandLine 'git', 'rev-list', 'master', '--count'
			standardOutput = stdout
		}
		return stdout.toString().trim()
	}
	buildnumber = 21

	update = "https://github.com/lazylazuli/Pipes/releases/download/Latest/update.json"
}

version = "$mcversion-$modversion-$buildnumber"
group = "com.github.lazylazuli.pipes"
archivesBaseName = modid

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
	sourceCompatibility = targetCompatibility = "1.8"
}

repositories {
	maven { url "https://jitpack.io" }
}

dependencies {
	compile "com.github.lazylazuli:LazyLazuliLib:$libVersion"
}

minecraft {
	version = "$mcversion-$forgeversion"
	runDir = "run"

	mappings = "snapshot_20161220"

	replace '@version@', modversion
	replace '@mcversion@', project.minecraft.version
	replace '@update@', update
	replaceIn modfile
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		expand 'version': modversion, 'mcversion': project.minecraft.version, 'modid': modid, 'modname': modname
	}

	// copy everything else except the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}